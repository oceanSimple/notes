# â™¾ï¸å•å…ƒæµ‹è¯•
## ğŸ’«åŸºç¡€

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class ToolTest {
    @TestCase
    public func testStringToInt() {
        let test1 = "123"
        @Expect(stringToInt(test1),123)

        let test2 = "123a"
        @ExpectThrows[Exception](stringToInt(test2))
    }
}
```

- @Expect
- @ExpectThrows

## ğŸ’«æƒ…å†µè®¨è®º
### æœ‰è¿”å›å€¼çš„å‡½æ•°(åŸºç¡€)
```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class ToolTest {
    @TestCase
    public func testStringToInt() {
        let test1 = "123"
        @Expect(stringToInt(test1),123)

        let test2 = "123a"
        @ExpectThrows[Exception](stringToInt(test2))
    }
}
```


# â™¾ï¸æ–‡ä»¶

```java
import std.fs.*
import std.io.*
import std.process.*
```

## ğŸ’«æ–‡ä»¶è·¯å¾„
> è·å–src/config.json

```java
import std.process.*

let curProcee = Process.current
let filePath = curProcee.workingDirectory.join("src").join("config.json")
if (!exists(filePath)) {
	throw FileNotExistException(filePath.toString())
}
```

## ğŸ’«è¯»å†™æ–‡ä»¶
> è¯»
```java
    let file = File(filePath, OpenMode.Read)

    try (myfile = StringReader(file)) {
        let info = myfile.readToEnd().toString()
    } finally {
        file.close()
    }
```

> å†™

```java
    let file = File(filePath, OpenMode.Write)
    try {
        let dm = cfg.serialize()
        let json = dm.toJson().toString()
        file.write(json.toArray())
    } finally {
        file.close()
    }
```


# â™¾ï¸å‡½æ•°
## ğŸ’«lambdaè¡¨è¾¾å¼
```java
        trie.addRoute(
            "GET",
            "/get/test",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )
```


# â™¾ï¸web
## ğŸ’«client
```java
import net.http
private func parseJwtTokenByHeader(token: String): Bool {
    let client = http.ClientBuilder().build()

    let req = http
        .HttpRequestBuilder()
        .method("GET")
        .header("jwt", token)
        .url("http://localhost:7000/jwt/verify")
        .build()

    let rsp = client.send(req)
    client.close()

    if (rsp.status == 200) {
        return true
    }
    return false
}
```

## ğŸ’«read request body
```java
import o_proxy_server.model.web.Context
import std.io.StringReader

protected func readBody(ctx: Context): String {
    let body = ctx.request.body
    let reader = StringReader(body)
    return reader.readToEnd()
}
```

## ğŸ’«distributor
> ç”¨äºè‡ªå®šä¹‰è·¯ç”±å¤„ç†å™¨
> æ³¨æ„, è¯¥ç¤ºä¾‹æ²¡æœ‰å¸¦ä¸Šè·¯ç”±æ ‘, å¦‚æœè¦æ ¹æ®methodå’Œgetè·å–handler, åˆ™éœ€è¦é€‰æ‹©è·¯ç”±æ ‘æ’ä»¶

```java
import net.http
import o_proxy_server.model.web.Context

public class HttpDistributor <: http.HttpRequestDistributor {
    public func distribute(_: String): http.HttpRequestHandler {
        return RootHandler()
    }

    public func register(_: String, _: (http.HttpContext) -> Unit): Unit {
    }

    public func register(_: String, _: http.HttpRequestHandler): Unit {
    }
}

public class RootHandler <: http.HttpRequestHandler {
    public func handle(ctx: http.HttpContext): Unit {
        let myCtx = Context(ctx.request, ctx.responseBuilder, HANDLERS)
        myCtx.next()
    }
}
```

## ğŸ’«server

```java
public class HttpServer {
    public var server: Server
    public var addr: String = "0.0.0.0"
    public var port: UInt16 = 8080

    public init() {
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public init(port: UInt16) {
        this.port = port
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public init(addr: String, port: UInt16) {
        this.addr = addr
        this.port = port
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public func updateToHttpsServer(crtName: String, keyName: String) {
        // 1. è‡ªå®šä¹‰é…ç½®
        // tcp é…ç½®
        var transportCfg = TransportConfig()
        transportCfg.readBufferSize = 8192
        // tls é…ç½® éœ€è¦ä¼ å…¥é…å¥—çš„è¯ä¹¦ä¸ç§é’¥æ–‡ä»¶è·¯å¾„
        let srcPath = Process.current.workingDirectory.join("src")

        let pem0 = String.fromUtf8(readToEnd(File(srcPath.join(crtName), Read)))
        let pem02 = String.fromUtf8(readToEnd(File(srcPath.join(keyName), Read)))
        var tlsConfig = TlsServerConfig(X509Certificate.decodeFromPem(pem0), PrivateKey.decodeFromPem(pem02))
        tlsConfig.supportedAlpnProtocols = ["h2"]

        // 2. æ„å»º Server å®ä¾‹
        this.port = 443
        this.server = ServerBuilder()
            .addr(addr)
            .port(this.port)
            .transportConfig(transportCfg)
            .tlsConfig(tlsConfig)
            .headerTableSize(10 * 1024)
            .maxRequestHeaderSize(1024 * 1024)
            .distributor(HttpDistributor())
            .build()
    }

    public func run() {
        if (this.port == 443) {
            println(LOG.info("HTTPS server is running on https://${addr}:${port}"))
        } else {
            println(LOG.info("HTTP server is running on http://${addr}:${port}"))
        }
        server.serve()
    }
}
```

## ğŸ’«ä¸­é—´ä»¶é€»è¾‘
> æ­é…è‡ªå®šä¹‰Contextä½¿ç”¨

```java
public func getMiddlewareHandler(): ArrayList<(Context) -> Unit> {
    let handlers = ArrayList<(Context) -> Unit>()

    // admin api æ’ä»¶
    handlers.add(adminApiApiHandler)

    // jwt æ’ä»¶
    handlers.add(jwtHandler)

    // è·¯ç”±é‡å†™æ’ä»¶
    handlers.add(rewriteHandler)

    // !!!æ³¨æ„, memoryCacheHandler å’Œ gatewayHandler çš„é¡ºåºä¸èƒ½é¢ å€’
    handlers.add(memoryCacheHandler)
    handlers.add(gatewayHandler)

    // è¿™ä¸ªå¿…é¡»æ˜¯æœ€åä¸€ä¸ª
    // é€€å‡ºå‡½æ•°
    handlers.add(exitHandler)

    return handlers
}
```

## ğŸ’«è¯»å–è¯·æ±‚å¤´æ•°æ®
```java
private func getToken(ctx: Context): ?String {
    let authorization = ctx.request.headers.get("Authorization")
    if (authorization.isEmpty()) {
        return None
    }
    for (str in authorization) {
        if (str.startsWith("Bearer ")) {
            return str.removePrefix("Bearer ")
        }
    }
    return None
}
```


# â™¾ï¸å…¶ä»–
## ğŸ’«jsonåºåˆ—åŒ–
> ç¤ºä¾‹
> æ³¨æ„, å¦‚æœæŸäº›jsonå­—æ®µæ˜¯å¯é€‰çš„, ç”¨try-catchæ•è·å¹¶èµ‹äºˆé»˜è®¤å€¼!!!

```java
import serialization.serialization.*
import encoding.json.*

protected class HttpsOptions <: Serializable<HttpsOptions> {
    var https: Bool
    var key: String
    var cert: String

    public init() {
        this.https = false
        this.key = ""
        this.cert = ""
    }

    public init(https: Bool, key: String, cert: String) {
        this.https = https
        this.key = key
        this.cert = cert
    }

    public func serialize(): DataModel {
        return DataModelStruct()
            .add(field<Bool>("https", https))
            .add(field<String>("key", key))
            .add(field<String>("cert", cert))
    }

    public static func deserialize(dm: DataModel): HttpsOptions {
        let dms = match (dm) {
            case data: DataModelStruct => data
            case _ => throw Exception("Invalid data model")
        }
        let result = HttpsOptions()

        result.key = String.deserialize(dms.get("key"))
        result.cert = String.deserialize(dms.get("cert"))

        // https is optional
        try {
            result.https = Bool.deserialize(dms.get("https"))
        } catch (e: DataModelException) {
            result.https = true
        }

        return result
    }
}
```

> å½“ç„¶ä¹Ÿæœ‰å·¥å…·ç½‘ç«™: http://json2cangjie.cn/
> ä½†è¯¥ç½‘ç«™ä¸æ”¯æŒè‡ªåŠ¨try-catch

## ğŸ’«jwtåº“
> å¯¼å…¥åº“ cjpm.toml

```toml
[dependencies]
  jwt4cj = {git = "https://gitcode.com/Cangjie-TPC/jwt4cj.git"}
```

> æ›´æ–°é…ç½®æ–‡ä»¶: cjpm update
- import
```java
import jwt4cj.*
import std.time.*
```
- åŸºç¡€çš„ç­¾å‘jwt
```java
	public func sign(): String {
		let jwtStr = JWT
			.create()
			.withExpiresAt(DateTime.now().addDays(1))
			.withIssuedAt(DateTime.now())
			.sign(Algorithm.HMAC256(this.secret))
	
		return jwtStr
	}
```

- åŸºç¡€çš„æ ¡éªŒ
```java
	// éªŒè¯jwt
	// è¿”å›å€¼: 1: éªŒè¯é€šè¿‡, 0: token è¿‡æœŸ, -1: éªŒè¯å¤±è´¥
	public func verify(token: String): Int64 {
		let require = JWT.require(Algorithm.HMAC256(this.secret))
		let verifier: JWTVerifier = require.build()
		try {
			let _ = verifier.verify(token)
			return 1
		} catch (e: TokenExpiredException) {
			return 0
		} catch (e: Exception) {
			return -1
		}
	}
```



## ğŸ’«è‡ªå®šä¹‰log

```java
import std.time
import std.collection.{ArrayList, forEach}

public class Log {
    public static let STRBUILDER = StringBuilder()
    public static let PATTERN = time.DateTimeFormat.of("yyyy-MM-dd HH:mm:ss")

    public func getLog(arr: Array<String>): String {
        STRBUILDER.reset()

        // print time
        STRBUILDER.append("[${getTime()}] ")

        arr |> forEach {p => STRBUILDER.append("${p} ")}

        // STRBUILDER.append("\n")
        return STRBUILDER.toString()
    }

    public func getTime(): String {
        let timer = time.DateTime.now().toString(PATTERN)
        return timer
    }

    public func info(arr: Array<String>): String {
        return getLog(appendParam("[INFO]", arr))
    }

    public func success(arr: Array<String>): String {
        return getLog(appendParam("[SUCCESS]", arr))
    }

    public func error(arr: Array<String>): String {
        return getLog(appendParam("[ERROR]", arr))
    }

    public func default(arr: Array<String>): String {
        return getLog(appendParam("[DEFAULT]", arr))
    }

    public func debug(arr: Array<String>): String {
        return getLog(appendParam("[DEBUG]", arr))
    }

    // å› ä¸ºæ²¡æ‰¾åˆ°ä»“é¢‰æ€ä¹ˆè§£æå¯å˜å‚æ•°ï¼Œæ‰€ä»¥è¿™é‡Œåªèƒ½ç”¨è¿™ç§ç¬¨æ–¹æ³•
    private func appendParam(prefix: String, arr: Array<String>): Array<String> {
        let result = ArrayList<String>(1 + arr.size)
        result.add(prefix)

        arr |> forEach {p => result.add(p)}

        return result.toArray()
    }
}

```

## ğŸ’«Exception
```java
public class FileNotExistException <: Exception {
    public init(filePath: String) {
        super("File not found: ${filePath}")
    }
}
```

## ğŸ’«random

```java
import std.random.Random

private let randomBuidler = Random(1)
let index = randomBuidler.nextInt64(member.group.size)
```


# â™¾ï¸ä»£ç ç‰‡æ®µ
## ğŸ’«string to int
```java
// å°†å­—ç¬¦ä¸²è½¬æ¢ä¸ºæ•´æ•°
public func stringToInt(s: String): Int64 {
    var result = 0
    for (c in s.toRuneArray()) {
        let index = UInt32(c) - 48
        if (index < 0 || index > 9) {
            throw Exception("Invalid character")
        }
        result = result * 10 + Int64(index)
    }
    return result
}
```

## ğŸ’«dequeue
```java
import std.deriving.*

@Derive[Equatable]
public class Node<T> where T <: Equatable<T> {
    protected var key: String
    protected var value: T
    protected var prev: ?Node<T>
    protected var next: ?Node<T>

    public init(key: String, value: T) {
        this.key = key
        this.value = value

        this.prev = None
        this.next = None
    }
}

public class DoubleList<T> where T <: Equatable<T> {
    private var head: ?Node<T>
    private var tail: ?Node<T>
    protected var size: Int64

    public init(initNode: Node<T>) {
        this.head = initNode
        this.tail = initNode
        head.getOrThrow().next = tail
        tail.getOrThrow().prev = head
        this.size = 0
    }

    public func front(): ?Node<T> {
        if (isEmpty()) {
            return None
        }
        return this.head.getOrThrow().next
    }

    public func back(): ?Node<T> {
        if (isEmpty()) {
            return None
        }
        return this.tail.getOrThrow().prev
    }

    // public func isEnd(node: Node<T>): Bool {
    //     if (isEmpty()) {
    //         return false
    //     }
    //     // å› ä¸ºæˆ‘ä»¬åˆ¤æ–­äº†isEmpty, æ‰€ä»¥è¿™é‡Œä¸ä¼šå‡ºç°None
    //     return node == back().getOrThrow()
    // }

    public func isEmpty(): Bool {
        return this.size == 0
    }

    public func getSize(): Int64 {
        return this.size
    }

    // åœ¨é“¾è¡¨å¤´éƒ¨æ·»åŠ èŠ‚ç‚¹
    public func addFirst(node: Node<T>) {
        node.prev = this.head
        node.next = this.head.getOrThrow().next

        this.head.getOrThrow().next.getOrThrow().prev = node
        this.head.getOrThrow().next = node

        this.size++
    }

    // åœ¨é“¾è¡¨å°¾éƒ¨æ·»åŠ èŠ‚ç‚¹
    public func addLast(node: Node<T>) {
        // initå‡½æ•°ç¡®ä¿äº†headå’Œtailä¸ä¸ºNone
        node.prev = this.tail.getOrThrow().prev
        node.next = this.tail

        this.tail.getOrThrow().prev.getOrThrow().next = node
        this.tail.getOrThrow().prev = node

        this.size++
    }

    // åˆ é™¤é“¾è¡¨ä¸­çš„nodeèŠ‚ç‚¹
    // è¿™é‡Œçš„nodeä¸€å®šå­˜åœ¨(å› ä¸ºæ˜¯ä»mapä¸­å–å‡ºæ¥çš„)
    public func remove(node: Node<T>) {
        node.prev.getOrThrow().next = node.next
        node.next.getOrThrow().prev = node.prev

        this.size--
    }

    // åˆ é™¤é“¾è¡¨ä¸­ç¬¬ä¸€ä¸ªèŠ‚ç‚¹å¹¶è¿”å›
    public func removeFirst(): ?Node<T> {
        // å¦‚æœheadä¸tailç›¸åŒ, è¯´æ˜é“¾è¡¨ä¸ºç©º
        if (this.head.getOrThrow().next.getOrThrow() == (this.tail.getOrThrow())) {
            return None
        }

        // è·å–ç¬¬ä¸€ä¸ªèŠ‚ç‚¹
        let first = this.head.getOrThrow().next.getOrThrow()
        // åˆ é™¤ç¬¬ä¸€ä¸ªèŠ‚ç‚¹
        this.remove(first)
        return first
    }
}
```

> test

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class DequeueTest {
    @TestCase
    public func testAddFirst() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        queue.addFirst(Node<Int64>("", 1))
        queue.addFirst(Node<Int64>("", 2))
        queue.addFirst(Node<Int64>("", 3))

        @Expect(queue.front().getOrThrow().value, 3)
        @Expect(queue.back().getOrThrow().value, 1)
    }

    @TestCase
    public func testAddLast() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        queue.addLast(Node<Int64>("", 1))
        queue.addLast(Node<Int64>("", 2))
        queue.addLast(Node<Int64>("", 3))

        @Expect(queue.front().getOrThrow().value, 1)
        @Expect(queue.back().getOrThrow().value, 3)
    }

    @TestCase
    public func testIsEmpty() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        @Expect(queue.isEmpty(), true)

        queue.addLast(Node<Int64>("", 1))
        @Expect(queue.isEmpty(), false)
    }

    @TestCase
    public func testForEach() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        let node1 = Node<Int64>("", 1)
        let node2 = Node<Int64>("", 2)
        let node3 = Node<Int64>("", 3)

        queue.addLast(node1)
        queue.addLast(node2)
        queue.addLast(node3)

        var index = queue.front().getOrThrow()
        for (i in 0..queue.getSize()) {
            @Expect(index.value, i+1)
            index = index.next.getOrThrow()
        }
    }

    @TestCase
    public func testRemove() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        let node1 = Node<Int64>("", 1)
        let node2 = Node<Int64>("", 2)
        let node3 = Node<Int64>("", 3)

        queue.addLast(node1)
        queue.addLast(node2)
        queue.addLast(node3)

        // åˆ é™¤node2
        queue.remove(node2)
        @Expect(queue.getSize(), 2)
        @Expect(queue.front().getOrThrow().value, 1)
        @Expect(queue.back().getOrThrow().value, 3)
    }
}
```

## ğŸ’«è‡ªå®šä¹‰Context
```ad-info
- å‚æ•°æ ¸å¿ƒ
	- public let request: http.HttpRequest
	- public let response: http.HttpResponseBuilder
	- public let handlers: collection.ArrayList<(Context) -> Unit>

- æ–¹æ³•æ ¸å¿ƒ(æ³¨æ„, è¯¥å®ä¾‹æ˜¯æ”¹ç‰ˆåçš„, é€šå¸¸terminateæ˜¯ç›´æ¥ç»“æŸæ‰€æœ‰ä¸­é—´ä»¶çš„è°ƒç”¨)
	- next(): ä¸‹ä¸€ä¸ªä¸­é—´ä»¶
	- terminate(): ç›´æ¥è¿›è¡Œæœ€åä¸€ä¸ªä¸­é—´ä»¶çš„è°ƒç”¨(é€šå¸¸æ˜¯exitä¸­é—´ä»¶)
	- end(): ç»“æŸæ‰€æœ‰çš„ä¸­é—´ä»¶è°ƒç”¨
```
```java
import net.http
import std.collection
import std.io.StringReader

public class Context {
    public let request: http.HttpRequest
    public let response: http.HttpResponseBuilder
    public var path: String
    public var method: String
    public var params: collection.HashMap<String, String> // path params

    // å“åº”ç›¸å…³å­—æ®µ
    public var statusCode: ?UInt16 = None // ä¿å­˜è¯·æ±‚è½¬å‘åresponseçš„çŠ¶æ€ç 
    public var rspType: ?String = None // ä¿å­˜è¯·æ±‚è½¬å‘åresponseçš„Content-Type

    // å†…å­˜ç¼“å­˜éœ€è¦çš„å­—æ®µ
    public var rspBody: ?String = None // ä¿å­˜è¯·æ±‚è½¬å‘åresponseçš„body
    public var cacheFlag: Bool = false // ç”¨æ¥è¡¨ç¤ºæ˜¯å¦è¿›è¡Œmemoryç¼“å­˜çš„æ ‡å¿—
    public var isHtml: Bool = false // ä¿å­˜è¯·æ±‚è½¬å‘åresponseçš„Content-Typeæ˜¯å¦ä¸ºtext/html

    // public let body: String
    public let handlers: collection.ArrayList<(Context) -> Unit>
    protected var index: Int64

    public init(request: http.HttpRequest, response: http.HttpResponseBuilder,
        handlers: collection.ArrayList<(Context) -> Unit>) {
        this.request = request
        this.response = response
        this.path = this.request.url.toString()
        this.method = this.request.method
        this.params = collection.HashMap<String, String>()
        // this.statusCode = 200
        // this.body = StringReader(request.body).readToEnd()
        this.handlers = handlers
        this.index = -1
    }

    // æ‰§è¡Œä¸‹ä¸€ä¸ªä¸­é—´ä»¶
    public func next() {
        this.index++
        while (index < this.handlers.size) {
            this.handlers[this.index](this)
            this.index++
        }
    }

    // ä¸­æ­¢åç»­ä¸­é—´ä»¶çš„æ‰§è¡Œ
    // ç›´æ¥è·³åˆ°æœ€åä¸€ä¸ªé€€å‡ºå‡½æ•°
    public func terminate() {
        // å› ä¸ºåœ¨next()å‡½æ•°ä¸­, ä¼šå…ˆåŠ 1, æ‰€ä»¥è¿™é‡Œå‡2
        this.index = this.handlers.size - 2
    }

    // ç»“æŸä¸­é—´ä»¶çš„æ‰§è¡Œ, åŒ…æ‹¬è·³è¿‡æœ€åä¸€ä¸ªé€€å‡ºå‡½æ•°
    public func end() {
        this.index = this.handlers.size
    }

    // html 200
    public func html200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "text/html"
    }

    // json 200
    public func json200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "application/json"
    }

    // text 200
    public func text200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "text/plain"
    }
}
```

## ğŸ’«å‰ç¼€è·¯ç”±æ ‘
### Node

```java
import std.collection.ArrayList

public class Node <: ToString {
    protected var pattern: String = "" // complete url path, like /p/:lang
    protected var part: String = "" // part of url path, like p, :lang
    protected let children: ArrayList<Node> = ArrayList() // children of this node
    protected var isWild: Bool = false // is exact match(if cantains : or *)

    protected func matchChild(part: String): ?Node {
        for (child in children) {
            if (child.part == part || child.isWild) {
                return child
            }
        }
        return Option<Node>.None
    }

    protected func insert(pattern: String, parts: ArrayList<String>, height: Int): Unit {
        if (parts.size == height) {
            this.pattern = pattern
            return
        }
        let part = parts[height]
        var child = matchChild(part)
        if (child.isNone()) {
            child = Node()
            child.getOrThrow().part = part
            child.getOrThrow().isWild = part.startsWith(":") || part.startsWith("*")
            this.children.add(child.getOrThrow())
        }
        child.getOrThrow().insert(pattern, parts, height + 1)
    }

    protected func matchChildren(part: String): ArrayList<Node> {
        let nodes = ArrayList<Node>()
        for (child in children) {
            if (child.part == part || child.isWild) {
                nodes.add(child)
            }
        }
        return nodes
    }

    protected func search(parts: ArrayList<String>, height: Int64): ?Node {
        if (parts.size == height || this.part.startsWith("*")) {
            if (this.pattern == "") {
                return Option<Node>.None
            }
            return this
        }
        let part = parts[height]
        let children = matchChildren(part)
        for (child in children) {
            let result = child.search(parts, height + 1)
            if (result.isSome()) {
                return result
            }
        }
        return None
    }

    public func toString(): String {
        return "Node{pattern=" + pattern + ", part=" + part + ", children=" + children.toString() + ", isWild=" +
            isWild.toString() + "}"
    }
}
```

### trie
```java
import std.collection.{HashMap, ArrayList}

// è·¯ç”±æ ‘
// !!!ä¸æ”¯æŒqueryå‚æ•°, ä¾‹å¦‚: /get?name=1, å¦‚æœéœ€è¦æ”¯æŒqueryå‚æ•°, éœ€è¦åœ¨Contextä¸­è§£æqueryå‚æ•°
// æ”¯æŒpathå‚æ•°, ä¾‹å¦‚: /get/:name/:age
// æ”¯æŒé€šé…ç¬¦, ä¾‹å¦‚: /get/*
// æ”¯æŒpathå‚æ•°å’Œé€šé…ç¬¦æ··åˆ, ä¾‹å¦‚: /get/:name/*
public class Trie {
    public var roots: HashMap<String, Node>
    public var handlers: HashMap<String, (Context) -> Unit>

    public init() {
        this.roots = HashMap<String, Node>()
        this.handlers = HashMap<String, (Context) -> Unit>()
    }

    public func pathAndHandler(c: Context): (?String, ?(Context) -> Unit) {
        // åˆ é™¤urlä¸­çš„queryå‚æ•°, åŒæ—¶è§£æpathå‚æ•°
        let url = c.request.url.toString()
        let urlArr = url.split("?", 2, removeEmpty: false)
        let (node, pathParam) = getRoute(c.request.method, urlArr[0])
        if (pathParam.isSome()) {
            c.params = pathParam.getOrThrow()
        }

        if (node.isSome()) {
            let key = c.request.method + "-" + node.getOrDefault({=> Node()}).pattern
            return (node.getOrThrow().pattern, handlers[key])
        }
        return (None, None)
    }

    // è¿”å›å€¼: (Node, HashMap<String, String>)
    // Node: åŒ¹é…åˆ°çš„èŠ‚ç‚¹, node.patternæ˜¯å®Œæ•´çš„urlè·¯å¾„
    // HashMap<String, String>: åŒ¹é…åˆ°çš„pathå‚æ•°
    protected func getRoute(method: String, path: String): (?Node, ?HashMap<String, String>) {
        let searchParts = parsePattern(path)
        let params = HashMap<String, String>()

        let ok = roots.contains(method)
        if (!ok) {
            return (Option<Node>.None, Option<HashMap<String, String>>.None)
        }

        let n = roots[method].search(searchParts, 0)

        if (n.isSome()) {
            let parts = parsePattern(n.getOrDefault({=> Node()}).pattern)

            var i = 0
            while (i < parts.size) {
                if (parts[i].startsWith(":")) {
                    params.add(parts[i][1..], searchParts[i])
                }
                if (parts[i].startsWith("*") && parts[i].size > 1) {
                    let builder = StringBuilder()
                    builder.append(searchParts[i..])
                    builder.append("/")
                    params.add(parts[i][1..], builder.toString())
                    break
                }
                i++
            }

            return (n, params)
        }

        return (None, None)
    }

    protected func parsePattern(pattern: String): ArrayList<String> {
        let vs = pattern.split("/")
        let parts = ArrayList<String>()
        for (p in vs) {
            if (p != "") {
                parts.add(p)
                if (p.startsWith("*")) {
                    break
                }
            }
        }
        return parts
    }

    protected func addRoute(method: String, pattern: String, handler: (Context) -> Unit) {
        let parts = parsePattern(pattern)
        let key = method + "-" + pattern

        let ok = roots.contains(method)
        if (!ok) {
            roots[method] = Node()
        }
        roots[method].insert(pattern, parts, 0)
        handlers[key] = handler
    }
}
```

### trie_test

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class TrieTest {
    @TestCase
    public func testDefaultRoute() {
        let trie = Trie()
        trie.addRoute(
            "GET",
            "/get/test",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )
        trie.addRoute("POST", "/post/test", {
            _ =>
        })

        let (getRoute, _) = trie.getRoute("GET", "/get/test")
        let (postRoute, _) = trie.getRoute("POST", "/post/test")

        @Expect(getRoute.isSome(), true)
        @Expect(getRoute.getOrThrow().pattern, "/get/test")

        @Expect(postRoute.isSome(), true)
        @Expect(postRoute.getOrThrow().pattern, "/post/test")
    }

    @TestCase
    public func testGetPathParam() {
        let trie = Trie()
        trie.addRoute(
            "GET",
            "/get/:name/:age",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )

        let (getRoute, params) = trie.getRoute("GET", "/get/1/2")

        @Expect(getRoute.isSome(), true)
        @Expect(getRoute.getOrThrow().pattern, "/get/:name/:age")

        @Expect(params.isSome(), true)
        @Expect(params.getOrThrow()["name"], "1")
        @Expect(params.getOrThrow()["age"], "2")
    }
}
```

### åº”ç”¨handler
```java
import o_proxy_server.model.web.{Context, Trie}
import std.collection.HashMap
import o_proxy_server.global.GLOBAL_CONFIG

public class AdminApi {
    // è·¯ç”±æ ‘
    public var trie: Trie

    public init() {
        this.trie = Trie()
    }

    public func trieHandle(ctx: Context): Bool {
        // è·å–pathå’Œhandler
        // let (path, handler) = this.pathAndHandler(ctx)
        let (_, handler) = this.pathAndHandler(ctx)
        if (handler.isNone()) {
            return false
        }

        // ä»headerä¸­çš„oproxykeyè·å–å¯†é’¥
        let oproxyKey = ctx.request.headers.get("oproxykey")
        var keyFlag = false
        for (i in oproxyKey) {
            if (i == GLOBAL_CONFIG.getOrThrow().adminKey) {
                keyFlag = true
                break
            }
        }
        if (!keyFlag) {
            // è¿”å›"æ²¡æœ‰æƒé™"çš„å“åº”
            ctx.text401("[admin-api] admin api key error")
            // ç»ˆæ­¢åç»­ä¸­é—´ä»¶çš„æ‰§è¡Œ
            ctx.terminate()
            return false
        }

        // æ‰§è¡Œhandler
        handler.getOrThrow()(ctx)

        return true
    }

    public func addRoute(method: String, pattern: String, handler: (Context) -> Unit) {
        this.trie.addRoute(method, pattern, handler)
    }

    public func pathAndHandler(c: Context): (?String, ?(Context) -> Unit) {
        return this.trie.pathAndHandler(c)
    }
}
```

## ğŸ’«LRU
### DoubleList
```java
protected class DoubleList {
    private var head: ?Node
    private var tail: ?Node
    protected var size: Int64

    public init() {
        let initNode = Node("", "")
        this.head = initNode
        this.tail = initNode
        head.getOrThrow().next = tail
        tail.getOrThrow().prev = head
        this.size = 0
    }

    // åœ¨é“¾è¡¨å°¾éƒ¨æ·»åŠ èŠ‚ç‚¹
    public func addLast(node: Node) {
        // initå‡½æ•°ç¡®ä¿äº†headå’Œtailä¸ä¸ºNone
        node.prev = this.tail.getOrThrow().prev
        node.next = this.tail

        this.tail.getOrThrow().prev.getOrThrow().next = node
        this.tail.getOrThrow().prev = node

        this.size++
    }

    // åˆ é™¤é“¾è¡¨ä¸­çš„nodeèŠ‚ç‚¹
    // è¿™é‡Œçš„nodeä¸€å®šå­˜åœ¨(å› ä¸ºæ˜¯ä»mapä¸­å–å‡ºæ¥çš„)
    public func remove(node: Node) {
        node.prev.getOrThrow().next = node.next
        node.next.getOrThrow().prev = node.prev

        this.size--
    }

    // åˆ é™¤é“¾è¡¨ä¸­ç¬¬ä¸€ä¸ªèŠ‚ç‚¹å¹¶è¿”å›
    public func removeFirst(): ?Node {
        // å¦‚æœheadä¸tailç›¸åŒ, è¯´æ˜é“¾è¡¨ä¸ºç©º
        if (this.head.getOrThrow().next.getOrThrow().equal(this.tail.getOrThrow())) {
            return None
        }

        // è·å–ç¬¬ä¸€ä¸ªèŠ‚ç‚¹
        let first = this.head.getOrThrow().next.getOrThrow()
        // åˆ é™¤ç¬¬ä¸€ä¸ªèŠ‚ç‚¹
        this.remove(first)
        return first
    }
}
```

### Node
```java
protected class Node {
    protected var key: String
    protected var value: String
    protected var prev: ?Node
    protected var next: ?Node
    public init(key: String, value: String) {
        this.key = key
        this.value = value

        this.prev = None
        this.next = None
    }

    public func equal(node: Node): Bool {
        return this.key == node.key && this.value == node.value
    }
}
```

### handler

```java
import std.collection.{HashMap, ArrayList, LinkedList}

// å†…å­˜ç¼“å­˜
protected class MemoryCache<H> where H <: CacheHandler {
    protected let cacheData: CacheData
    protected let cacheHandler: H

    public init(capacity: Int64, cacheHandler: H) {
        this.cacheData = CacheData(capacity)
        this.cacheHandler = cacheHandler
    }

    // è·å–ç¼“å­˜æ•°æ®
    public func get(key: String): ?String {
        // é€šè¿‡å¤„ç†å™¨, åˆ¤æ–­ç¼“å­˜æ•°æ®æ˜¯å¦å­˜åœ¨
        let flag = this.cacheHandler.get(key)
        if (flag) {
            return this.cacheData.cache.get(key)
        } else {
            return None
        }
    }

    // è®¾ç½®ç¼“å­˜æ•°æ®
    public func put(key: String, value: String): Unit {
        // é¦–å…ˆå°†æ•°æ®æ”¾å…¥ç¼“å­˜å¤„ç†å™¨ä¸­
        this.cacheData.cache.add(key, value)

        // é€šè¿‡å¤„ç†å™¨, åˆ¤æ–­æ˜¯å¦éœ€è¦åˆ é™¤ç¼“å­˜æ•°æ®
        let deleteKey = this.cacheHandler.put(key, value, this.cacheData)
        if (deleteKey.isNone()) {
            return
        }
        this.cacheData.cache.remove(deleteKey.getOrThrow())
    }

    // æ‰“å°ç¼“å­˜æ•°æ®
    public func printCacheData(): Unit {
        for ((k, v) in this.cacheData.cache) {
            println("key: ${k}, value: ${v}")
        }
    }

    public func catCacheData(): ArrayList<String> {
        let result = ArrayList<String>()
        for ((k, v) in this.cacheData.cache) {
            result.add("${k}: ${v}")
        }
        return result
    }
}

// ç¼“å­˜æ•°æ®
protected class CacheData {
    // ä¿å­˜ç¼“å­˜æ•°æ®
    protected let cache: HashMap<String, String>
    // æœ€å¤§ç¼“å­˜æ•°
    protected var capacity: Int64
    // å½“å‰ç¼“å­˜æ•°
    protected var size: Int64

    public init(capacity: Int64) {
        this.capacity = capacity
        this.size = 0
        this.cache = HashMap<String, String>()
    }
}

// ç¼“å­˜å¤„ç†å™¨: ä¸åŒçš„ç¼“å­˜æ›¿æ¢ç­–ç•¥
protected interface CacheHandler {
    // è·å–ç¼“å­˜æ•°æ®
    // å› ä¸ºCacheDataä¸­ä¿å­˜äº†ç¼“å­˜æ•°æ®, æ‰€ä»¥å¤„ç†å™¨åªéœ€è¦å‘Šè¯‰CacheDataæ•°æ®æ˜¯å¦å­˜åœ¨å³å¯
    // å…¶ä»–ç¼“å­˜æ›¿æ¢ç­–ç•¥ç”±å¤„ç†å™¨è‡ªå·±å®ç°, ä¸CacheDataæ— å…³
    func get(key: String): Bool

    // è®¾ç½®ç¼“å­˜æ•°æ®
    // å› ä¸ºCacheDataåªå…·æœ‰ä¿å­˜ç¼“å­˜æ•°æ®çš„åŠŸèƒ½, å› æ­¤ä¸çŸ¥é“æœ¬æ¬¡putæ“ä½œæ˜¯å¦ä¼šå¯¼è‡´ç¼“å­˜æ›¿æ¢
    // å› æ­¤éœ€è¦å¤„ç†å™¨é€šè¿‡è¿”å›å€¼, å‘Šè¯‰CacheDataæ˜¯å¦éœ€è¦æ›¿æ¢ç¼“å­˜
    func put(key: String, value: String, cacheData: CacheData): ?String
}

```

```java
protected class LruHandler <: CacheHandler {
    private let cacheList: DoubleList
    private let map: HashMap<String, Node>

    public init() {
        this.cacheList = DoubleList()
        this.map = HashMap()
    }

    // è¿”å›æ˜¯å¦å­˜åœ¨key-value
    public func get(key: String): Bool {
        // åˆ¤æ–­keyæ˜¯å¦å­˜åœ¨
        let value = this.map.get(key)
        if (value.isNone()) {
            return false
        }

        // å¦‚æœkeyå­˜åœ¨, å…ˆé€šè¿‡makerecentlyæå‡keyçš„ä½ç½®
        this.makerecently(key)

        return true
    }

    public func put(key: String, value: String, cacheData: CacheData): ?String {
        // æ ¹æ®keyè·å–node
        let node = this.map.get(key)

        // å¦‚æœkeyå­˜åœ¨
        // åªéœ€è¦æ›´æ–°value, å¹¶æå‡ä¸ºæœ€è¿‘ä½¿ç”¨çš„. ä¸éœ€è¦è¿›è¡Œç¼“å­˜æ›¿æ¢
        if (node.isSome()) {
            // åˆ é™¤æ—§çš„æ•°æ®
            deleteKey(key)
            // æ–°æ’å…¥çš„æ•°æ®ä¸ºæœ€è¿‘ä½¿ç”¨çš„æ•°æ®
            addRecently(key, value)
            return None
        }

        // å¦‚æœkeyä¸å­˜åœ¨
        // éœ€è¦åˆ¤æ–­ç¼“å­˜æ˜¯å¦å·²æ»¡
        var deleteKey: ?String = None
        if (cacheList.size == cacheData.capacity) {
            // åˆ é™¤æœ€ä¹…æœªä½¿ç”¨çš„å…ƒç´ 
            deleteKey = removeLeastRecently()
        }

        // æ·»åŠ æœ€è¿‘ä½¿ç”¨çš„å…ƒç´ 
        addRecently(key, value)

        return deleteKey
    }

    // å°†æŸä¸ªkeyæå‡ä¸ºæœ€è¿‘ä½¿ç”¨çš„
    // å¦‚æœkeyä¸å­˜åœ¨, åˆ™è¿”å›false
    private func makerecently(key: String): Bool {
        let node = this.map.get(key)
        // ä¿è¯nodeå­˜åœ¨
        if (node.isNone()) {
            return false
        }
        // ä»é“¾è¡¨ä¸­åˆ é™¤node
        this.cacheList.remove(node.getOrThrow())
        // é‡æ–°æ’å…¥åˆ°é“¾è¡¨å°¾éƒ¨
        this.cacheList.addLast(node.getOrThrow())

        return true
    }

    // æ·»åŠ æœ€è¿‘ä½¿ç”¨çš„å…ƒç´ 
    private func addRecently(key: String, value: String): Unit {
        let node = Node(key, value)
        // é“¾è¡¨å°¾éƒ¨å°±æ˜¯æœ€è¿‘ä½¿ç”¨çš„å…ƒç´ 
        this.cacheList.addLast(node)
        // åœ¨mapä¸­æ·»åŠ keyå’Œnodeçš„æ˜ å°„
        this.map.add(key, node)
    }

    // åˆ é™¤æŸä¸€ä¸ªkey
    // å¦‚æœkeyä¸å­˜åœ¨, åˆ™è¿”å›false
    private func deleteKey(key: String): Bool {
        let node = this.map.get(key)
        if (node.isNone()) {
            return false
        }
        // ä»é“¾è¡¨ä¸­åˆ é™¤
        this.cacheList.remove(node.getOrThrow())
        // ä»mapä¸­åˆ é™¤
        this.map.remove(key)

        return true
    }

    // åˆ é™¤æœ€ä¹…æœªä½¿ç”¨çš„å…ƒç´ 
    private func removeLeastRecently(): ?String {
        // é“¾è¡¨å¤´éƒ¨çš„ç¬¬ä¸€ä¸ªå…ƒç´ å°±æ˜¯æœ€ä¹…æœªä½¿ç”¨çš„
        let node = this.cacheList.removeFirst()
        // å¦‚æœå¾—åˆ°çš„æ˜¯None, è¯´æ˜é“¾è¡¨ä¸ºç©º, ç›´æ¥è¿”å›
        if (node.isNone()) {
            return None
        }
        // ä»mapä¸­åˆ é™¤
        this.map.remove(node.getOrThrow().key)

        return node.getOrThrow().key
    }
}
```

### test
```java
import std.unittest.*
import std.unittest.testmacro.*
import std.collection.{ ArrayList}

@Test
public class CacheTest {
    @TestCase
    public func testLRU() {
        // åˆ›å»ºä¸€ä¸ªLRUç¼“å­˜å¤„ç†å™¨
        let lruHandler = LruHandler()
        // åˆ›å»ºç¼“å­˜
        let memoryCache = MemoryCache(3, lruHandler)

        // æ·»åŠ æ•°æ®
        memoryCache.put("1", "1")
        memoryCache.put("2", "2")
        memoryCache.put("3", "3")
        var cacheData = memoryCache.catCacheData()

        var predictData = ArrayList<String>()
        predictData.add("1: 1")
        predictData.add("2: 2")
        predictData.add("3: 3")

        for(i in 0..3) {
            @Expect(cacheData.get(i), predictData.get(i))
        }

        // æ·»åŠ æ•°æ®
        memoryCache.put("1", "1")
        memoryCache.put("4", "4")

        predictData = ArrayList<String>()
        predictData.add("1: 1")
        predictData.add("3: 3")
        predictData.add("4: 4")
        

        cacheData = memoryCache.catCacheData()
        for(i in 0..3) {
            @Expect(cacheData.get(i), predictData.get(i))
        }
    }
}
```


## ğŸ’«$1,$2æ›¿æ¢
```java
        // æ›¿æ¢rewrite.replacementä¸­çš„$1, $2ç­‰
        var str = rewrite.getOrThrow().replacement
        let rule = "(\\$\\d+)"
        let regex = Regex(rule)
        var matcher = regex.matcher(str)

        let result = matcher.findAll()
        if (result.isNone()) {
            return str
        }
        for (r in result.getOrThrow()) {
            let index = stringToInt(r.matchStr()[1..]) - 1
            if (index < matchArray.size && index >= 0) {
                str = str.replace(r.matchStr(), matchArray[index])
            }
        }
```















