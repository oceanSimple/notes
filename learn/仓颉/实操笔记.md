# ♾️单元测试
## 💫基础

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class ToolTest {
    @TestCase
    public func testStringToInt() {
        let test1 = "123"
        @Expect(stringToInt(test1),123)

        let test2 = "123a"
        @ExpectThrows[Exception](stringToInt(test2))
    }
}
```

- @Expect
- @ExpectThrows

## 💫情况讨论
### 有返回值的函数(基础)
```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class ToolTest {
    @TestCase
    public func testStringToInt() {
        let test1 = "123"
        @Expect(stringToInt(test1),123)

        let test2 = "123a"
        @ExpectThrows[Exception](stringToInt(test2))
    }
}
```


# ♾️文件

```java
import std.fs.*
import std.io.*
import std.process.*
```

## 💫文件路径
> 获取src/config.json

```java
import std.process.*

let curProcee = Process.current
let filePath = curProcee.workingDirectory.join("src").join("config.json")
if (!exists(filePath)) {
	throw FileNotExistException(filePath.toString())
}
```

## 💫读写文件
> 读
```java
    let file = File(filePath, OpenMode.Read)

    try (myfile = StringReader(file)) {
        let info = myfile.readToEnd().toString()
    } finally {
        file.close()
    }
```

> 写

```java
    let file = File(filePath, OpenMode.Write)
    try {
        let dm = cfg.serialize()
        let json = dm.toJson().toString()
        file.write(json.toArray())
    } finally {
        file.close()
    }
```


# ♾️函数
## 💫lambda表达式
```java
        trie.addRoute(
            "GET",
            "/get/test",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )
```


# ♾️web
## 💫client
```java
import net.http
private func parseJwtTokenByHeader(token: String): Bool {
    let client = http.ClientBuilder().build()

    let req = http
        .HttpRequestBuilder()
        .method("GET")
        .header("jwt", token)
        .url("http://localhost:7000/jwt/verify")
        .build()

    let rsp = client.send(req)
    client.close()

    if (rsp.status == 200) {
        return true
    }
    return false
}
```

## 💫read request body
```java
import o_proxy_server.model.web.Context
import std.io.StringReader

protected func readBody(ctx: Context): String {
    let body = ctx.request.body
    let reader = StringReader(body)
    return reader.readToEnd()
}
```

## 💫distributor
> 用于自定义路由处理器
> 注意, 该示例没有带上路由树, 如果要根据method和get获取handler, 则需要选择路由树插件

```java
import net.http
import o_proxy_server.model.web.Context

public class HttpDistributor <: http.HttpRequestDistributor {
    public func distribute(_: String): http.HttpRequestHandler {
        return RootHandler()
    }

    public func register(_: String, _: (http.HttpContext) -> Unit): Unit {
    }

    public func register(_: String, _: http.HttpRequestHandler): Unit {
    }
}

public class RootHandler <: http.HttpRequestHandler {
    public func handle(ctx: http.HttpContext): Unit {
        let myCtx = Context(ctx.request, ctx.responseBuilder, HANDLERS)
        myCtx.next()
    }
}
```

## 💫server

```java
public class HttpServer {
    public var server: Server
    public var addr: String = "0.0.0.0"
    public var port: UInt16 = 8080

    public init() {
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public init(port: UInt16) {
        this.port = port
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public init(addr: String, port: UInt16) {
        this.addr = addr
        this.port = port
        server = ServerBuilder().addr(addr).port(port).distributor(HttpDistributor()).build()
    }

    public func updateToHttpsServer(crtName: String, keyName: String) {
        // 1. 自定义配置
        // tcp 配置
        var transportCfg = TransportConfig()
        transportCfg.readBufferSize = 8192
        // tls 配置 需要传入配套的证书与私钥文件路径
        let srcPath = Process.current.workingDirectory.join("src")

        let pem0 = String.fromUtf8(readToEnd(File(srcPath.join(crtName), Read)))
        let pem02 = String.fromUtf8(readToEnd(File(srcPath.join(keyName), Read)))
        var tlsConfig = TlsServerConfig(X509Certificate.decodeFromPem(pem0), PrivateKey.decodeFromPem(pem02))
        tlsConfig.supportedAlpnProtocols = ["h2"]

        // 2. 构建 Server 实例
        this.port = 443
        this.server = ServerBuilder()
            .addr(addr)
            .port(this.port)
            .transportConfig(transportCfg)
            .tlsConfig(tlsConfig)
            .headerTableSize(10 * 1024)
            .maxRequestHeaderSize(1024 * 1024)
            .distributor(HttpDistributor())
            .build()
    }

    public func run() {
        if (this.port == 443) {
            println(LOG.info("HTTPS server is running on https://${addr}:${port}"))
        } else {
            println(LOG.info("HTTP server is running on http://${addr}:${port}"))
        }
        server.serve()
    }
}
```

## 💫中间件逻辑
> 搭配自定义Context使用

```java
public func getMiddlewareHandler(): ArrayList<(Context) -> Unit> {
    let handlers = ArrayList<(Context) -> Unit>()

    // admin api 插件
    handlers.add(adminApiApiHandler)

    // jwt 插件
    handlers.add(jwtHandler)

    // 路由重写插件
    handlers.add(rewriteHandler)

    // !!!注意, memoryCacheHandler 和 gatewayHandler 的顺序不能颠倒
    handlers.add(memoryCacheHandler)
    handlers.add(gatewayHandler)

    // 这个必须是最后一个
    // 退出函数
    handlers.add(exitHandler)

    return handlers
}
```

## 💫读取请求头数据
```java
private func getToken(ctx: Context): ?String {
    let authorization = ctx.request.headers.get("Authorization")
    if (authorization.isEmpty()) {
        return None
    }
    for (str in authorization) {
        if (str.startsWith("Bearer ")) {
            return str.removePrefix("Bearer ")
        }
    }
    return None
}
```


# ♾️其他
## 💫json序列化
> 示例
> 注意, 如果某些json字段是可选的, 用try-catch捕获并赋予默认值!!!

```java
import serialization.serialization.*
import encoding.json.*

protected class HttpsOptions <: Serializable<HttpsOptions> {
    var https: Bool
    var key: String
    var cert: String

    public init() {
        this.https = false
        this.key = ""
        this.cert = ""
    }

    public init(https: Bool, key: String, cert: String) {
        this.https = https
        this.key = key
        this.cert = cert
    }

    public func serialize(): DataModel {
        return DataModelStruct()
            .add(field<Bool>("https", https))
            .add(field<String>("key", key))
            .add(field<String>("cert", cert))
    }

    public static func deserialize(dm: DataModel): HttpsOptions {
        let dms = match (dm) {
            case data: DataModelStruct => data
            case _ => throw Exception("Invalid data model")
        }
        let result = HttpsOptions()

        result.key = String.deserialize(dms.get("key"))
        result.cert = String.deserialize(dms.get("cert"))

        // https is optional
        try {
            result.https = Bool.deserialize(dms.get("https"))
        } catch (e: DataModelException) {
            result.https = true
        }

        return result
    }
}
```

> 当然也有工具网站: http://json2cangjie.cn/
> 但该网站不支持自动try-catch

## 💫jwt库
> 导入库 cjpm.toml

```toml
[dependencies]
  jwt4cj = {git = "https://gitcode.com/Cangjie-TPC/jwt4cj.git"}
```

> 更新配置文件: cjpm update
- import
```java
import jwt4cj.*
import std.time.*
```
- 基础的签发jwt
```java
	public func sign(): String {
		let jwtStr = JWT
			.create()
			.withExpiresAt(DateTime.now().addDays(1))
			.withIssuedAt(DateTime.now())
			.sign(Algorithm.HMAC256(this.secret))
	
		return jwtStr
	}
```

- 基础的校验
```java
	// 验证jwt
	// 返回值: 1: 验证通过, 0: token 过期, -1: 验证失败
	public func verify(token: String): Int64 {
		let require = JWT.require(Algorithm.HMAC256(this.secret))
		let verifier: JWTVerifier = require.build()
		try {
			let _ = verifier.verify(token)
			return 1
		} catch (e: TokenExpiredException) {
			return 0
		} catch (e: Exception) {
			return -1
		}
	}
```



## 💫自定义log

```java
import std.time
import std.collection.{ArrayList, forEach}

public class Log {
    public static let STRBUILDER = StringBuilder()
    public static let PATTERN = time.DateTimeFormat.of("yyyy-MM-dd HH:mm:ss")

    public func getLog(arr: Array<String>): String {
        STRBUILDER.reset()

        // print time
        STRBUILDER.append("[${getTime()}] ")

        arr |> forEach {p => STRBUILDER.append("${p} ")}

        // STRBUILDER.append("\n")
        return STRBUILDER.toString()
    }

    public func getTime(): String {
        let timer = time.DateTime.now().toString(PATTERN)
        return timer
    }

    public func info(arr: Array<String>): String {
        return getLog(appendParam("[INFO]", arr))
    }

    public func success(arr: Array<String>): String {
        return getLog(appendParam("[SUCCESS]", arr))
    }

    public func error(arr: Array<String>): String {
        return getLog(appendParam("[ERROR]", arr))
    }

    public func default(arr: Array<String>): String {
        return getLog(appendParam("[DEFAULT]", arr))
    }

    public func debug(arr: Array<String>): String {
        return getLog(appendParam("[DEBUG]", arr))
    }

    // 因为没找到仓颉怎么解析可变参数，所以这里只能用这种笨方法
    private func appendParam(prefix: String, arr: Array<String>): Array<String> {
        let result = ArrayList<String>(1 + arr.size)
        result.add(prefix)

        arr |> forEach {p => result.add(p)}

        return result.toArray()
    }
}

```

## 💫Exception
```java
public class FileNotExistException <: Exception {
    public init(filePath: String) {
        super("File not found: ${filePath}")
    }
}
```

## 💫random

```java
import std.random.Random

private let randomBuidler = Random(1)
let index = randomBuidler.nextInt64(member.group.size)
```


# ♾️代码片段
## 💫string to int
```java
// 将字符串转换为整数
public func stringToInt(s: String): Int64 {
    var result = 0
    for (c in s.toRuneArray()) {
        let index = UInt32(c) - 48
        if (index < 0 || index > 9) {
            throw Exception("Invalid character")
        }
        result = result * 10 + Int64(index)
    }
    return result
}
```

## 💫dequeue
```java
import std.deriving.*

@Derive[Equatable]
public class Node<T> where T <: Equatable<T> {
    protected var key: String
    protected var value: T
    protected var prev: ?Node<T>
    protected var next: ?Node<T>

    public init(key: String, value: T) {
        this.key = key
        this.value = value

        this.prev = None
        this.next = None
    }
}

public class DoubleList<T> where T <: Equatable<T> {
    private var head: ?Node<T>
    private var tail: ?Node<T>
    protected var size: Int64

    public init(initNode: Node<T>) {
        this.head = initNode
        this.tail = initNode
        head.getOrThrow().next = tail
        tail.getOrThrow().prev = head
        this.size = 0
    }

    public func front(): ?Node<T> {
        if (isEmpty()) {
            return None
        }
        return this.head.getOrThrow().next
    }

    public func back(): ?Node<T> {
        if (isEmpty()) {
            return None
        }
        return this.tail.getOrThrow().prev
    }

    // public func isEnd(node: Node<T>): Bool {
    //     if (isEmpty()) {
    //         return false
    //     }
    //     // 因为我们判断了isEmpty, 所以这里不会出现None
    //     return node == back().getOrThrow()
    // }

    public func isEmpty(): Bool {
        return this.size == 0
    }

    public func getSize(): Int64 {
        return this.size
    }

    // 在链表头部添加节点
    public func addFirst(node: Node<T>) {
        node.prev = this.head
        node.next = this.head.getOrThrow().next

        this.head.getOrThrow().next.getOrThrow().prev = node
        this.head.getOrThrow().next = node

        this.size++
    }

    // 在链表尾部添加节点
    public func addLast(node: Node<T>) {
        // init函数确保了head和tail不为None
        node.prev = this.tail.getOrThrow().prev
        node.next = this.tail

        this.tail.getOrThrow().prev.getOrThrow().next = node
        this.tail.getOrThrow().prev = node

        this.size++
    }

    // 删除链表中的node节点
    // 这里的node一定存在(因为是从map中取出来的)
    public func remove(node: Node<T>) {
        node.prev.getOrThrow().next = node.next
        node.next.getOrThrow().prev = node.prev

        this.size--
    }

    // 删除链表中第一个节点并返回
    public func removeFirst(): ?Node<T> {
        // 如果head与tail相同, 说明链表为空
        if (this.head.getOrThrow().next.getOrThrow() == (this.tail.getOrThrow())) {
            return None
        }

        // 获取第一个节点
        let first = this.head.getOrThrow().next.getOrThrow()
        // 删除第一个节点
        this.remove(first)
        return first
    }
}
```

> test

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class DequeueTest {
    @TestCase
    public func testAddFirst() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        queue.addFirst(Node<Int64>("", 1))
        queue.addFirst(Node<Int64>("", 2))
        queue.addFirst(Node<Int64>("", 3))

        @Expect(queue.front().getOrThrow().value, 3)
        @Expect(queue.back().getOrThrow().value, 1)
    }

    @TestCase
    public func testAddLast() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        queue.addLast(Node<Int64>("", 1))
        queue.addLast(Node<Int64>("", 2))
        queue.addLast(Node<Int64>("", 3))

        @Expect(queue.front().getOrThrow().value, 1)
        @Expect(queue.back().getOrThrow().value, 3)
    }

    @TestCase
    public func testIsEmpty() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        @Expect(queue.isEmpty(), true)

        queue.addLast(Node<Int64>("", 1))
        @Expect(queue.isEmpty(), false)
    }

    @TestCase
    public func testForEach() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        let node1 = Node<Int64>("", 1)
        let node2 = Node<Int64>("", 2)
        let node3 = Node<Int64>("", 3)

        queue.addLast(node1)
        queue.addLast(node2)
        queue.addLast(node3)

        var index = queue.front().getOrThrow()
        for (i in 0..queue.getSize()) {
            @Expect(index.value, i+1)
            index = index.next.getOrThrow()
        }
    }

    @TestCase
    public func testRemove() {
        let queue = DoubleList<Int64>(Node<Int64>("", 0))
        let node1 = Node<Int64>("", 1)
        let node2 = Node<Int64>("", 2)
        let node3 = Node<Int64>("", 3)

        queue.addLast(node1)
        queue.addLast(node2)
        queue.addLast(node3)

        // 删除node2
        queue.remove(node2)
        @Expect(queue.getSize(), 2)
        @Expect(queue.front().getOrThrow().value, 1)
        @Expect(queue.back().getOrThrow().value, 3)
    }
}
```

## 💫自定义Context
```ad-info
- 参数核心
	- public let request: http.HttpRequest
	- public let response: http.HttpResponseBuilder
	- public let handlers: collection.ArrayList<(Context) -> Unit>

- 方法核心(注意, 该实例是改版后的, 通常terminate是直接结束所有中间件的调用)
	- next(): 下一个中间件
	- terminate(): 直接进行最后一个中间件的调用(通常是exit中间件)
	- end(): 结束所有的中间件调用
```
```java
import net.http
import std.collection
import std.io.StringReader

public class Context {
    public let request: http.HttpRequest
    public let response: http.HttpResponseBuilder
    public var path: String
    public var method: String
    public var params: collection.HashMap<String, String> // path params

    // 响应相关字段
    public var statusCode: ?UInt16 = None // 保存请求转发后response的状态码
    public var rspType: ?String = None // 保存请求转发后response的Content-Type

    // 内存缓存需要的字段
    public var rspBody: ?String = None // 保存请求转发后response的body
    public var cacheFlag: Bool = false // 用来表示是否进行memory缓存的标志
    public var isHtml: Bool = false // 保存请求转发后response的Content-Type是否为text/html

    // public let body: String
    public let handlers: collection.ArrayList<(Context) -> Unit>
    protected var index: Int64

    public init(request: http.HttpRequest, response: http.HttpResponseBuilder,
        handlers: collection.ArrayList<(Context) -> Unit>) {
        this.request = request
        this.response = response
        this.path = this.request.url.toString()
        this.method = this.request.method
        this.params = collection.HashMap<String, String>()
        // this.statusCode = 200
        // this.body = StringReader(request.body).readToEnd()
        this.handlers = handlers
        this.index = -1
    }

    // 执行下一个中间件
    public func next() {
        this.index++
        while (index < this.handlers.size) {
            this.handlers[this.index](this)
            this.index++
        }
    }

    // 中止后续中间件的执行
    // 直接跳到最后一个退出函数
    public func terminate() {
        // 因为在next()函数中, 会先加1, 所以这里减2
        this.index = this.handlers.size - 2
    }

    // 结束中间件的执行, 包括跳过最后一个退出函数
    public func end() {
        this.index = this.handlers.size
    }

    // html 200
    public func html200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "text/html"
    }

    // json 200
    public func json200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "application/json"
    }

    // text 200
    public func text200(body: String) {
        this.statusCode = 200
        this.rspBody = body
        this.rspType = "text/plain"
    }
}
```

## 💫前缀路由树
### Node

```java
import std.collection.ArrayList

public class Node <: ToString {
    protected var pattern: String = "" // complete url path, like /p/:lang
    protected var part: String = "" // part of url path, like p, :lang
    protected let children: ArrayList<Node> = ArrayList() // children of this node
    protected var isWild: Bool = false // is exact match(if cantains : or *)

    protected func matchChild(part: String): ?Node {
        for (child in children) {
            if (child.part == part || child.isWild) {
                return child
            }
        }
        return Option<Node>.None
    }

    protected func insert(pattern: String, parts: ArrayList<String>, height: Int): Unit {
        if (parts.size == height) {
            this.pattern = pattern
            return
        }
        let part = parts[height]
        var child = matchChild(part)
        if (child.isNone()) {
            child = Node()
            child.getOrThrow().part = part
            child.getOrThrow().isWild = part.startsWith(":") || part.startsWith("*")
            this.children.add(child.getOrThrow())
        }
        child.getOrThrow().insert(pattern, parts, height + 1)
    }

    protected func matchChildren(part: String): ArrayList<Node> {
        let nodes = ArrayList<Node>()
        for (child in children) {
            if (child.part == part || child.isWild) {
                nodes.add(child)
            }
        }
        return nodes
    }

    protected func search(parts: ArrayList<String>, height: Int64): ?Node {
        if (parts.size == height || this.part.startsWith("*")) {
            if (this.pattern == "") {
                return Option<Node>.None
            }
            return this
        }
        let part = parts[height]
        let children = matchChildren(part)
        for (child in children) {
            let result = child.search(parts, height + 1)
            if (result.isSome()) {
                return result
            }
        }
        return None
    }

    public func toString(): String {
        return "Node{pattern=" + pattern + ", part=" + part + ", children=" + children.toString() + ", isWild=" +
            isWild.toString() + "}"
    }
}
```

### trie
```java
import std.collection.{HashMap, ArrayList}

// 路由树
// !!!不支持query参数, 例如: /get?name=1, 如果需要支持query参数, 需要在Context中解析query参数
// 支持path参数, 例如: /get/:name/:age
// 支持通配符, 例如: /get/*
// 支持path参数和通配符混合, 例如: /get/:name/*
public class Trie {
    public var roots: HashMap<String, Node>
    public var handlers: HashMap<String, (Context) -> Unit>

    public init() {
        this.roots = HashMap<String, Node>()
        this.handlers = HashMap<String, (Context) -> Unit>()
    }

    public func pathAndHandler(c: Context): (?String, ?(Context) -> Unit) {
        // 删除url中的query参数, 同时解析path参数
        let url = c.request.url.toString()
        let urlArr = url.split("?", 2, removeEmpty: false)
        let (node, pathParam) = getRoute(c.request.method, urlArr[0])
        if (pathParam.isSome()) {
            c.params = pathParam.getOrThrow()
        }

        if (node.isSome()) {
            let key = c.request.method + "-" + node.getOrDefault({=> Node()}).pattern
            return (node.getOrThrow().pattern, handlers[key])
        }
        return (None, None)
    }

    // 返回值: (Node, HashMap<String, String>)
    // Node: 匹配到的节点, node.pattern是完整的url路径
    // HashMap<String, String>: 匹配到的path参数
    protected func getRoute(method: String, path: String): (?Node, ?HashMap<String, String>) {
        let searchParts = parsePattern(path)
        let params = HashMap<String, String>()

        let ok = roots.contains(method)
        if (!ok) {
            return (Option<Node>.None, Option<HashMap<String, String>>.None)
        }

        let n = roots[method].search(searchParts, 0)

        if (n.isSome()) {
            let parts = parsePattern(n.getOrDefault({=> Node()}).pattern)

            var i = 0
            while (i < parts.size) {
                if (parts[i].startsWith(":")) {
                    params.add(parts[i][1..], searchParts[i])
                }
                if (parts[i].startsWith("*") && parts[i].size > 1) {
                    let builder = StringBuilder()
                    builder.append(searchParts[i..])
                    builder.append("/")
                    params.add(parts[i][1..], builder.toString())
                    break
                }
                i++
            }

            return (n, params)
        }

        return (None, None)
    }

    protected func parsePattern(pattern: String): ArrayList<String> {
        let vs = pattern.split("/")
        let parts = ArrayList<String>()
        for (p in vs) {
            if (p != "") {
                parts.add(p)
                if (p.startsWith("*")) {
                    break
                }
            }
        }
        return parts
    }

    protected func addRoute(method: String, pattern: String, handler: (Context) -> Unit) {
        let parts = parsePattern(pattern)
        let key = method + "-" + pattern

        let ok = roots.contains(method)
        if (!ok) {
            roots[method] = Node()
        }
        roots[method].insert(pattern, parts, 0)
        handlers[key] = handler
    }
}
```

### trie_test

```java
import std.unittest.*
import std.unittest.testmacro.*

@Test
public class TrieTest {
    @TestCase
    public func testDefaultRoute() {
        let trie = Trie()
        trie.addRoute(
            "GET",
            "/get/test",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )
        trie.addRoute("POST", "/post/test", {
            _ =>
        })

        let (getRoute, _) = trie.getRoute("GET", "/get/test")
        let (postRoute, _) = trie.getRoute("POST", "/post/test")

        @Expect(getRoute.isSome(), true)
        @Expect(getRoute.getOrThrow().pattern, "/get/test")

        @Expect(postRoute.isSome(), true)
        @Expect(postRoute.getOrThrow().pattern, "/post/test")
    }

    @TestCase
    public func testGetPathParam() {
        let trie = Trie()
        trie.addRoute(
            "GET",
            "/get/:name/:age",
            {
                ctx =>
                ctx.text200("get test")
                ctx.next()
            }
        )

        let (getRoute, params) = trie.getRoute("GET", "/get/1/2")

        @Expect(getRoute.isSome(), true)
        @Expect(getRoute.getOrThrow().pattern, "/get/:name/:age")

        @Expect(params.isSome(), true)
        @Expect(params.getOrThrow()["name"], "1")
        @Expect(params.getOrThrow()["age"], "2")
    }
}
```

### 应用handler
```java
import o_proxy_server.model.web.{Context, Trie}
import std.collection.HashMap
import o_proxy_server.global.GLOBAL_CONFIG

public class AdminApi {
    // 路由树
    public var trie: Trie

    public init() {
        this.trie = Trie()
    }

    public func trieHandle(ctx: Context): Bool {
        // 获取path和handler
        // let (path, handler) = this.pathAndHandler(ctx)
        let (_, handler) = this.pathAndHandler(ctx)
        if (handler.isNone()) {
            return false
        }

        // 从header中的oproxykey获取密钥
        let oproxyKey = ctx.request.headers.get("oproxykey")
        var keyFlag = false
        for (i in oproxyKey) {
            if (i == GLOBAL_CONFIG.getOrThrow().adminKey) {
                keyFlag = true
                break
            }
        }
        if (!keyFlag) {
            // 返回"没有权限"的响应
            ctx.text401("[admin-api] admin api key error")
            // 终止后续中间件的执行
            ctx.terminate()
            return false
        }

        // 执行handler
        handler.getOrThrow()(ctx)

        return true
    }

    public func addRoute(method: String, pattern: String, handler: (Context) -> Unit) {
        this.trie.addRoute(method, pattern, handler)
    }

    public func pathAndHandler(c: Context): (?String, ?(Context) -> Unit) {
        return this.trie.pathAndHandler(c)
    }
}
```

## 💫LRU
### DoubleList
```java
protected class DoubleList {
    private var head: ?Node
    private var tail: ?Node
    protected var size: Int64

    public init() {
        let initNode = Node("", "")
        this.head = initNode
        this.tail = initNode
        head.getOrThrow().next = tail
        tail.getOrThrow().prev = head
        this.size = 0
    }

    // 在链表尾部添加节点
    public func addLast(node: Node) {
        // init函数确保了head和tail不为None
        node.prev = this.tail.getOrThrow().prev
        node.next = this.tail

        this.tail.getOrThrow().prev.getOrThrow().next = node
        this.tail.getOrThrow().prev = node

        this.size++
    }

    // 删除链表中的node节点
    // 这里的node一定存在(因为是从map中取出来的)
    public func remove(node: Node) {
        node.prev.getOrThrow().next = node.next
        node.next.getOrThrow().prev = node.prev

        this.size--
    }

    // 删除链表中第一个节点并返回
    public func removeFirst(): ?Node {
        // 如果head与tail相同, 说明链表为空
        if (this.head.getOrThrow().next.getOrThrow().equal(this.tail.getOrThrow())) {
            return None
        }

        // 获取第一个节点
        let first = this.head.getOrThrow().next.getOrThrow()
        // 删除第一个节点
        this.remove(first)
        return first
    }
}
```

### Node
```java
protected class Node {
    protected var key: String
    protected var value: String
    protected var prev: ?Node
    protected var next: ?Node
    public init(key: String, value: String) {
        this.key = key
        this.value = value

        this.prev = None
        this.next = None
    }

    public func equal(node: Node): Bool {
        return this.key == node.key && this.value == node.value
    }
}
```

### handler

```java
import std.collection.{HashMap, ArrayList, LinkedList}

// 内存缓存
protected class MemoryCache<H> where H <: CacheHandler {
    protected let cacheData: CacheData
    protected let cacheHandler: H

    public init(capacity: Int64, cacheHandler: H) {
        this.cacheData = CacheData(capacity)
        this.cacheHandler = cacheHandler
    }

    // 获取缓存数据
    public func get(key: String): ?String {
        // 通过处理器, 判断缓存数据是否存在
        let flag = this.cacheHandler.get(key)
        if (flag) {
            return this.cacheData.cache.get(key)
        } else {
            return None
        }
    }

    // 设置缓存数据
    public func put(key: String, value: String): Unit {
        // 首先将数据放入缓存处理器中
        this.cacheData.cache.add(key, value)

        // 通过处理器, 判断是否需要删除缓存数据
        let deleteKey = this.cacheHandler.put(key, value, this.cacheData)
        if (deleteKey.isNone()) {
            return
        }
        this.cacheData.cache.remove(deleteKey.getOrThrow())
    }

    // 打印缓存数据
    public func printCacheData(): Unit {
        for ((k, v) in this.cacheData.cache) {
            println("key: ${k}, value: ${v}")
        }
    }

    public func catCacheData(): ArrayList<String> {
        let result = ArrayList<String>()
        for ((k, v) in this.cacheData.cache) {
            result.add("${k}: ${v}")
        }
        return result
    }
}

// 缓存数据
protected class CacheData {
    // 保存缓存数据
    protected let cache: HashMap<String, String>
    // 最大缓存数
    protected var capacity: Int64
    // 当前缓存数
    protected var size: Int64

    public init(capacity: Int64) {
        this.capacity = capacity
        this.size = 0
        this.cache = HashMap<String, String>()
    }
}

// 缓存处理器: 不同的缓存替换策略
protected interface CacheHandler {
    // 获取缓存数据
    // 因为CacheData中保存了缓存数据, 所以处理器只需要告诉CacheData数据是否存在即可
    // 其他缓存替换策略由处理器自己实现, 与CacheData无关
    func get(key: String): Bool

    // 设置缓存数据
    // 因为CacheData只具有保存缓存数据的功能, 因此不知道本次put操作是否会导致缓存替换
    // 因此需要处理器通过返回值, 告诉CacheData是否需要替换缓存
    func put(key: String, value: String, cacheData: CacheData): ?String
}

```

```java
protected class LruHandler <: CacheHandler {
    private let cacheList: DoubleList
    private let map: HashMap<String, Node>

    public init() {
        this.cacheList = DoubleList()
        this.map = HashMap()
    }

    // 返回是否存在key-value
    public func get(key: String): Bool {
        // 判断key是否存在
        let value = this.map.get(key)
        if (value.isNone()) {
            return false
        }

        // 如果key存在, 先通过makerecently提升key的位置
        this.makerecently(key)

        return true
    }

    public func put(key: String, value: String, cacheData: CacheData): ?String {
        // 根据key获取node
        let node = this.map.get(key)

        // 如果key存在
        // 只需要更新value, 并提升为最近使用的. 不需要进行缓存替换
        if (node.isSome()) {
            // 删除旧的数据
            deleteKey(key)
            // 新插入的数据为最近使用的数据
            addRecently(key, value)
            return None
        }

        // 如果key不存在
        // 需要判断缓存是否已满
        var deleteKey: ?String = None
        if (cacheList.size == cacheData.capacity) {
            // 删除最久未使用的元素
            deleteKey = removeLeastRecently()
        }

        // 添加最近使用的元素
        addRecently(key, value)

        return deleteKey
    }

    // 将某个key提升为最近使用的
    // 如果key不存在, 则返回false
    private func makerecently(key: String): Bool {
        let node = this.map.get(key)
        // 保证node存在
        if (node.isNone()) {
            return false
        }
        // 从链表中删除node
        this.cacheList.remove(node.getOrThrow())
        // 重新插入到链表尾部
        this.cacheList.addLast(node.getOrThrow())

        return true
    }

    // 添加最近使用的元素
    private func addRecently(key: String, value: String): Unit {
        let node = Node(key, value)
        // 链表尾部就是最近使用的元素
        this.cacheList.addLast(node)
        // 在map中添加key和node的映射
        this.map.add(key, node)
    }

    // 删除某一个key
    // 如果key不存在, 则返回false
    private func deleteKey(key: String): Bool {
        let node = this.map.get(key)
        if (node.isNone()) {
            return false
        }
        // 从链表中删除
        this.cacheList.remove(node.getOrThrow())
        // 从map中删除
        this.map.remove(key)

        return true
    }

    // 删除最久未使用的元素
    private func removeLeastRecently(): ?String {
        // 链表头部的第一个元素就是最久未使用的
        let node = this.cacheList.removeFirst()
        // 如果得到的是None, 说明链表为空, 直接返回
        if (node.isNone()) {
            return None
        }
        // 从map中删除
        this.map.remove(node.getOrThrow().key)

        return node.getOrThrow().key
    }
}
```

### test
```java
import std.unittest.*
import std.unittest.testmacro.*
import std.collection.{ ArrayList}

@Test
public class CacheTest {
    @TestCase
    public func testLRU() {
        // 创建一个LRU缓存处理器
        let lruHandler = LruHandler()
        // 创建缓存
        let memoryCache = MemoryCache(3, lruHandler)

        // 添加数据
        memoryCache.put("1", "1")
        memoryCache.put("2", "2")
        memoryCache.put("3", "3")
        var cacheData = memoryCache.catCacheData()

        var predictData = ArrayList<String>()
        predictData.add("1: 1")
        predictData.add("2: 2")
        predictData.add("3: 3")

        for(i in 0..3) {
            @Expect(cacheData.get(i), predictData.get(i))
        }

        // 添加数据
        memoryCache.put("1", "1")
        memoryCache.put("4", "4")

        predictData = ArrayList<String>()
        predictData.add("1: 1")
        predictData.add("3: 3")
        predictData.add("4: 4")
        

        cacheData = memoryCache.catCacheData()
        for(i in 0..3) {
            @Expect(cacheData.get(i), predictData.get(i))
        }
    }
}
```


## 💫$1,$2替换
```java
        // 替换rewrite.replacement中的$1, $2等
        var str = rewrite.getOrThrow().replacement
        let rule = "(\\$\\d+)"
        let regex = Regex(rule)
        var matcher = regex.matcher(str)

        let result = matcher.findAll()
        if (result.isNone()) {
            return str
        }
        for (r in result.getOrThrow()) {
            let index = stringToInt(r.matchStr()[1..]) - 1
            if (index < matchArray.size && index >= 0) {
                str = str.replace(r.matchStr(), matchArray[index])
            }
        }
```















